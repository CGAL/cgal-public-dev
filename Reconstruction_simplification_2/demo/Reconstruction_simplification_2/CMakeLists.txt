# This is the CMake script for compiling the Reconstruction_simplification_2 demo.

project(Reconstruction_simplification_2_demo)

cmake_minimum_required(VERSION 2.8.11)
if(POLICY CMP0043)
  cmake_policy(SET CMP0043 OLD)
endif()

set( CMAKE_ALLOW_LOOSE_LOOP_CONSTRUCTS true )

# Include this package's headers first
include_directories( BEFORE ./ ./include ../../include )

# Find CGAL and CGAL Qt5
find_package(CGAL COMPONENTS Qt5)
include( ${CGAL_USE_FILE} )

# Find Qt5 itself
find_package(Qt5 COMPONENTS Xml Script OpenGL Widgets)

# Find OpenGL
#find_package(OpenGL)

# Find QGLViewer
if(Qt5_FOUND)
  #add_definitions(-DQT_NO_KEYWORDS)
  find_package(QGLViewer)
endif(Qt5_FOUND)

# Find CImg
find_path(CIMG_INCLUDE_DIR
          NAMES CImg.h
          HINTS ENV CIMG_INC_DIR
          DOC "Path to the header of the CImg library")

if (CIMG_INCLUDE_DIR)
  add_definitions(-DCGAL_USE_CIMG)
  include_directories(${CIMG_INCLUDE_DIR})
  MESSAGE(STATUS "CImg library found, the demo can load point set from image files.")
else()
  MESSAGE(STATUS "CImg library was not found, the demo will not be able to load point set. "
                 "Try setting the environment variable CIMG_INC_DIR to point to the path of the directory containing CImg.h.")
endif()

if(CGAL_FOUND AND CGAL_Qt5_FOUND AND Qt5_FOUND AND OPENGL_FOUND AND QGLVIEWER_FOUND)

  include_directories ( ${QGLVIEWER_INCLUDE_DIR} )

  set(
  SRCS 
  glviewer.cpp 
  Reconstruction_simplification_2_demo.cpp 
  window.cpp 
  render.cpp 
  )

  set( 
  MOCS 
  moc_dialog_options.cxx 
  moc_glviewer.cxx 
  moc_window.cxx 
  )

  set(
  UIS
  pwsrec.ui 
  options.ui
  )
  
  qt5_wrap_ui( UI_FILES ${UIS} )

  include(AddFileDependencies)

  qt5_generate_moc( "window.h" "${CMAKE_CURRENT_BINARY_DIR}/moc_window.cxx" )
  add_file_dependencies( moc_window.cxx "${CMAKE_CURRENT_SOURCE_DIR}/window.h" )

  qt5_generate_moc( "glviewer.h" "${CMAKE_CURRENT_BINARY_DIR}/moc_glviewer.cxx" )
  add_file_dependencies( moc_glviewer.cxx "${CMAKE_CURRENT_SOURCE_DIR}/glviewer.h" )

  qt5_generate_moc( "dialog_options.h" "${CMAKE_CURRENT_BINARY_DIR}/moc_dialog_options.cxx" )
  add_file_dependencies( moc_dialog_options.cxx "${CMAKE_CURRENT_SOURCE_DIR}/dialog_options.h" )

  qt5_add_resources ( RESOURCE_FILES pwsrec.qrc )

  add_executable  ( Reconstruction_simplification_2_demo ${SRCS} ${MOCS} ${UI_FILES} ${RESOURCE_FILES} ${CGAL_ADDITIONAL_FILES} ${CGAL_RESOURCE_FILES})
  
  qt5_use_modules(Reconstruction_simplification_2_demo Xml Script OpenGL Widgets)

  # Link with Qt libraries
  target_link_libraries( Reconstruction_simplification_2_demo ${QT_LIBRARIES} )

  # Link with CGAL
  target_link_libraries( Reconstruction_simplification_2_demo ${CGAL_LIBRARIES} ${CGAL_3RD_PARTY_LIBRARIES} )

  # Link with libQGLViewer, OpenGL
  target_link_libraries( Reconstruction_simplification_2_demo ${QGLVIEWER_LIBRARIES} ${OPENGL_gl_LIBRARY} ${OPENGL_glu_LIBRARY} )

  # Link with CImg dependencies
  if( NOT WIN32 )
	  target_link_libraries( Reconstruction_simplification_2_demo -L/usr/X11R6/lib -lm -lpthread -lX11 )
  endif()
    
  add_to_cached_list( CGAL_EXECUTABLE_TARGETS Reconstruction_simplification_2_demo )

else (CGAL_FOUND AND CGAL_Qt5_FOUND AND Qt5_FOUND AND OPENGL_FOUND AND QGLVIEWER_FOUND)

  set(RS2_MISSING_DEPS "")

  if(NOT CGAL_FOUND)
    set(RS2_MISSING_DEPS "the CGAL library, ${RS2_MISSING_DEPS}")
  endif()

  if(NOT CGAL_Qt5_FOUND)
    set(RS2_MISSING_DEPS "the CGAL Qt5 library, ${RS2_MISSING_DEPS}")
  endif()

  if(NOT Qt5_FOUND)
    set(RS2_MISSING_DEPS "Qt5, ${RS2_MISSING_DEPS}")
  endif()

  if(NOT OPENGL_FOUND)
    set(RS2_MISSING_DEPS "OpenGL, ${RS2_MISSING_DEPS}")
  endif()

  if(NOT QGLVIEWER_FOUND)
    set(RS2_MISSING_DEPS "QGLViewer, ${RS2_MISSING_DEPS}")
  endif()

  message(STATUS "NOTICE: This demo requires ${RS2_MISSING_DEPS}and will not be compiled.")

endif (CGAL_FOUND AND CGAL_Qt5_FOUND AND Qt5_FOUND AND OPENGL_FOUND AND QGLVIEWER_FOUND)
